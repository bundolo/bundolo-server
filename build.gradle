buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE")
        //classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

mainClassName = "org.bundolo.Application"

jar {
    baseName = 'bundolo-server'
    version =  '1.2.7'
}

//tried this to get jar built as linux service. it required 1.3.5 spring-boot-gradle-plugin, which downloaded new spring, that didn't work with current setup.
//springBoot {
//    executable = true
//}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

// Test Structure
sourceSets {
    integration_test {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }

    acceptance_test {
        java.srcDir file('src/acceptance-test/java')
        resources.srcDir file('src/acceptance-test/resources')
    }
}

task integration_test(type: Test) {
    testClassesDir = sourceSets.integration_test.output.classesDir
    classpath = sourceSets.integration_test.runtimeClasspath
    enabled = true
    outputs.upToDateWhen {
    	false
    }
    testLogging {
        events 'started', 'passed', "standardOut"
        showStandardStreams = true
    }
    systemProperty 'env', 'dev'
    systemProperty 'newsletter.sender.schedule', '0 0 0 31 2 *'
}

task acceptance_test(type: Test) {
    testClassesDir = sourceSets.acceptance_test.output.classesDir
    classpath = sourceSets.acceptance_test.runtimeClasspath
    enabled = true
    outputs.upToDateWhen {
    	false
    }
    testLogging {
        events 'started', 'passed'
    }
}

check.dependsOn integration_test
check.dependsOn acceptance_test

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework:spring-tx")
    compile 'org.springframework:spring-orm:3.1.2.RELEASE'
    compile("org.springframework:spring-context-support")

    compile("org.springframework.security:spring-security-web")
    compile("org.springframework.security:spring-security-config")

    compile("com.fasterxml.jackson.core:jackson-databind")
    compile 'com.sun.mail:javax.mail:1.5.1'
    compile 'commons-codec:commons-codec:1.7'
    compile 'org.eclipse.persistence:javax.persistence:2.1.0'
    compile 'javax.validation:validation-api:1.0.0.GA'

	compile 'org.hibernate:hibernate-validator:4.3.1.Final'
	compile 'org.hibernate:hibernate-c3p0:4.1.5.SP1'
	compile 'org.hibernate:hibernate-entitymanager:4.1.5.SP1'
	compile 'org.hibernate:hibernate-core:4.1.5.SP1'

//TODO upgrading hibernate
//this was tested, and it gave unique constraint exception on saving comment
//  compile 'org.hibernate:hibernate-validator:5.1.0.Final'
//  compile 'org.hibernate:hibernate-c3p0:5.1.0.Final'
//	compile 'org.hibernate:hibernate-entitymanager:5.1.0.Final'
//	compile 'org.hibernate:hibernate-core:5.1.0.Final'

    compile 'postgresql:postgresql:9.1-901.jdbc3'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile 'org.freemarker:freemarker:2.3.22'

    //compile 'net.sf.dozer:dozer:5.5.1'

    //compile 'org.apache.pdfbox:pdfbox:2.0.3'

    testCompile 'org.springframework:spring-test'
    testCompile("junit:junit")
    testCompile ("org.seleniumhq.selenium:selenium-java:2.8.0")

    integration_testCompile sourceSets.main.output
    integration_testCompile configurations.testCompile
    integration_testCompile sourceSets.test.output
    integration_testRuntime configurations.testRuntime

    acceptance_testCompile sourceSets.main.output
    acceptance_testCompile configurations.testCompile
    acceptance_testCompile sourceSets.test.output
    acceptance_testRuntime configurations.testRuntime
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

